apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.triplet.play'

apply from: rootProject.file('gradle/generated-kotlin-sources.gradle')

apply plugin: 'jacoco'
apply plugin: 'com.getkeepsafe.dexcount'

apply plugin: 'org.jetbrains.kotlin.android.extensions'

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.24.4'
    }
}

apply plugin: 'io.fabric'

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url "https://maven.google.com"
    }
    maven { url 'https://maven.fabric.io/public' }
}

task copyTestClasses(type: Copy) {
    from "build/tmp/kotlin-classes/debugUnitTest"
    into "build/intermediates/classes/debug"
}

kapt {
    arguments {
        arg("room.schemaLocation", "$projectDir/schemas")
    }
}

def shortTag = "git rev-parse --short HEAD".execute().text.trim()

def beta = true

if(System.getenv("CIRCLE_TAG")) {
    beta = false
}

android {
    playAccountConfigs {
        defaultAccountConfig {
            jsonFile = file('LibreSubstratum.json')
        }
    }
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        buildConfigField "boolean", "BETA", beta.toString()
        def nextVersion = "0.2.1"
        applicationId "com.jereksel.libresubstratum"
        minSdkVersion 24
        targetSdkVersion 27
        if (System.getenv("CIRCLECI")) {
            def code = System.getenv("CIRCLE_BUILD_NUM").toInteger() - 250
            versionCode code
            if (System.getenv("CIRCLE_TAG")) {
                //Remove "v" from beginning (https://www.codesd.com/item/remove-the-prefix-from-the-string-in-groovy.html)
                def reg = ~/^v/
                versionName System.getenv("CIRCLE_TAG") - reg
            } else {
                versionName "${nextVersion}-BETA-${code}-${shortTag}"
            }
        } else if (project.hasProperty("fdroid")) {
            versionCode 3
            versionName nextVersion
        } else {
            versionCode 9999
            versionName "${nextVersion}-DEV"
        }

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

//        testInstrumentationRunner "com.jereksel.libresubstratum.TestAppAndroidJUnitRunner"
        manifestPlaceholders = [crashlytics: System.getenv("CRASHLYTICS_TOKEN") ?: ""]
        playAccountConfig = playAccountConfigs.defaultAccountConfig
        ndk {
            abiFilters 'armeabi-v7a', "arm64-v8a"
        }
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_PLATFORM=android-24',
                        '-DANDROID_TOOLCHAIN=clang', '-DANDROID_STL=gnustl_static'
            }
        }
    }
    signingConfigs {
        release {
            storeFile file('./LibreSubstratum.jks')
            storePassword System.getenv("storePassword")
            keyAlias System.getenv("keyAlias")
            keyPassword System.getenv("keyPassword")
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            testCoverageEnabled true
            ext.enableCrashlytics = false
        }
        debugProguarded {
            initWith(buildTypes.debug)
            minifyEnabled true
            ext.enableCrashlytics = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            matchingFallbacks = ['debug']
        }
    }
    flavorDimensions "store"
    productFlavors {
        play {
            dimension "store"
            versionNameSuffix "-play"
        }
        fdroid {
            dimension "store"
            versionNameSuffix "-fdroid"
        }
    }
    externalNativeBuild {
        cmake {
            path 'src/main/jni/CMakeLists.txt'
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/*.kotlin_module'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'

        //Arrow
        debug.java.srcDirs += 'build/generated/source/kaptKotlin/fdroidDebug'
//        release.java.srcDirs += 'build/generated/source/kaptKotlin/release'
    }
    dataBinding {
        enabled = true
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
            beforeTest { desc ->
                testLogging.showStandardStreams = true
                println "Executing test ${desc.name} [${desc.className}] with result: STARTED"
            }
            afterTest { desc, result ->
                testLogging.showStandardStreams = true
                println "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
            }
        }
    }
//    compileOptions {
//        sourceCompatibility JavaVersion.VERSION_1_8
//        targetCompatibility JavaVersion.VERSION_1_8
//    }

}

task jacocoTestReport(type: JacocoReport) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports"
    reports {
        xml.enabled = true
        html.enabled = true
    }
    def fileFilter = ["**/R.class",
                      "**/R\$*.class",
                      "**/BuildConfig.*",
                      "**/Manifest*.*",
                      "android/**/*.*",
                      "**/Lambda\$*.class", //Retrolambda
                      "**/Lambda.class",
                      "**/*Lambda.class",
                      "**/*Lambda*.class",
                      "**/*Lambda*.*",
                      "**/*Builder.*",
                      "**/*_MembersInjector.class", //Dagger2 generated code
                      "**/*_MembersInjector*.*", //Dagger2 generated code
                      "**/*_*Factory*.*", //Dagger2 generated code
                      "**/*Component*.*", //Dagger2 generated code
                      "**/*Module*.*" //Dagger2 generated code
    ]
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/fdroid/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"
    def kotlinSrc = "${project.projectDir}/src/main/kotlin"
    sourceDirectories = files([mainSrc, kotlinSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.projectDir, includes:
            ["**/*.exec" , "**/*.ec"])
}

dependencies {
    api kotlinLib

    //Support library
    supportLibrary.each { name, lib ->
        implementation lib
    }

    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutines_version}", {
    }
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:${coroutines_version}", {
    }
    api "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:${coroutines_version}", {
    }
    api "org.jetbrains.kotlinx:kotlinx-coroutines-guava:${coroutines_version}", {
        exclude group: "com.google.guava"
    }

    implementation "android.arch.lifecycle:extensions:1.0.0"
    implementation "android.arch.lifecycle:reactivestreams:1.0.0"
    testCompile "android.arch.core:core-testing:1.0.0"
    kapt 'com.android.databinding:compiler:3.0.1'

    compile "org.funktionale:funktionale-option:1.2"

    //RxStuff
    implementation rxjava
    implementation rxkotlin
    implementation rxandroid

//    testCompile 'com.rubylichtenstein:rxtest:1.0.5'

    api  "io.arrow-kt:arrow-optics:$arrow_version"
    kapt ("io.arrow-kt:arrow-annotations-processor:$arrow_version") {
        exclude group: "me.eugeniomarletti"
    }
    compileOnly ('me.eugeniomarletti:kotlin-metadata:1.2.1') {
//        force = true
    }

    //Dagger
    implementation dagger
    kapt daggerCompiler
    kaptTest daggerCompiler
    testCompile group: 'javax.inject', name: 'javax.inject', version: '1'

    implementation 'eu.chainfire:libsuperuser:1.0.0.201704021214'

    compile 'com.hannesdorfmann.mosby3:mvi:3.1.0'

    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    //Kotterknife
    implementation 'com.jakewharton:kotterknife:0.1.0-20170921.134610-22', {
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }

    implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'

    implementation 'com.github.clans:fab:1.6.4'
    implementation "com.github.akarnokd:rxjava2-extensions:0.18.1"
    implementation 'org.zeroturnaround:zt-zip:1.12'

    //Logging
    implementation "com.github.tony19:logback-android-core:$logback_android_version"
    implementation("com.github.tony19:logback-android-classic:$logback_android_version") {
        exclude group: 'com.google.android'
    }

    implementation slf4japi
    playCompile('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }

    implementation group: 'commons-codec', name: 'commons-codec', version: '1.11'

    implementation 'com.github.deano2390:MaterialShowcaseView:1.1.0@aar'

    implementation "com.github.marcinmoskala.activitystarter:activitystarter:$activity_starter_version"
    kapt "com.github.marcinmoskala.activitystarter:activitystarter-compiler:$activity_starter_version"

    implementation anko
    implementation ankoRecyclerView

    implementation 'com.github.kittinunf.result:result:1.2.0'

    //Signing
    implementation project(":zipsigner:combined")
    implementation project(":aapt:lib")

    implementation project(":libs:changelogdialog")

    implementation 'com.squareup.picasso:picasso:2.5.2'

    implementation 'com.jereksel:omslib:1.0.2'

    //So Proguard would shut up
    implementation 'com.google.j2objc:j2objc-annotations:1.3'
    implementation 'com.google.errorprone:error_prone_annotations:2.1.2'
    implementation guava
    androidTestCompile guava
//    compile 'com.android.tools.build:apksig:2.3.0-alpha3'

    implementation 'cat.ereza:customactivityoncrash:2.1.0'

    implementation "android.arch.persistence.room:runtime:$room_version"
    kapt "android.arch.persistence.room:compiler:$room_version"

//    implementation project(":sublib:compiler")
    implementation project(":sublib:models")
//    implementation project(":sublib:reader")
    implementation project(":sublib:themereaderassetmanager")
    api project(':sublib:compilerassetmanager')

    implementation "android.arch.lifecycle:runtime:1.0.3"

    //Testing
    testCompile junit

    testCompile kotlinReflect
    testCompile mockito
    testCompile robolectric

    //DON'T UPDATE: https://github.com/kotlintest/kotlintest/issues/174
    testCompile ("io.kotlintest:kotlintest:$kotlintest_version") {
        exclude group: "org.jetbrains.kotlin"
    }
    testCompile ("com.nhaarman:mockito-kotlin:$mockito_kotlin_version") {
        exclude group: "org.jetbrains.kotlin"
    }

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile ('com.android.support.test:runner:0.3', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile ('com.android.support.test:rules:0.3', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testCompile group: 'commons-io', name: 'commons-io', version: '2.5'
    testCompile group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
    androidTestCompile group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'

    //assertj android
    testCompile ("com.squareup.assertj:assertj-android:$assertj_android_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile "com.squareup.assertj:assertj-android-support-v4:$assertj_android_version@aar"
    testCompile "com.squareup.assertj:assertj-android-play-services:$assertj_android_version@aar"
    testCompile "com.squareup.assertj:assertj-android-appcompat-v7:$assertj_android_version@aar"
    testCompile "com.squareup.assertj:assertj-android-design:$assertj_android_version@aar"
    testCompile "com.squareup.assertj:assertj-android-mediarouter-v7:$assertj_android_version@aar"
    testCompile "com.squareup.assertj:assertj-android-gridlayout-v7:$assertj_android_version@aar"
    testCompile "com.squareup.assertj:assertj-android-cardview-v7:$assertj_android_version@aar"
    testCompile "com.squareup.assertj:assertj-android-recyclerview-v7:$assertj_android_version@aar"
    testCompile "com.squareup.assertj:assertj-android-palette-v7:$assertj_android_version@aar"


    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-intents:2.2.2", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    })
    compile project(path: ':sublib:compilercommon')
}

play {
    if (System.getenv("CIRCLE_TAG")) {
        track = 'production'
    } else {
        track = 'beta'
    }
    untrackOld = true
    uploadImages = true
}

androidExtensions {
    experimental = true
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

task testAll() {
    dependsOn("testFdroidDebugUnitTest")
    dependsOn(":sublib:colorreader:test")
    dependsOn(":sublib:compilerassetmanager:test")
    dependsOn(":sublib:themereaderassetmanager:test")
}
